// @file AM3352_icepick.gel
// @date 13 Aug 2015
// @author Chester Gillon
// @brief GEL file for the IcePick in the AM3352 SOM
// @details Upon connection forces the Cortex-A8 and its peripherals to the reset state,
//          so the debugger can connect regardless of the current state.

OnTargetConnect ()
{
    /* Enable wait-in-reset for the Cortex-A8, so that after a system reset the boot ROM
     * can't start to load a program.
     * 
     * "CortxA8" is mis-spelt to match the desc in
     * <ccs_install_directory>/ccsv6/ccs_base/common/targetdb/devices/AM3352.xml */
    GEL_EvalOnTarget ("CortxA8", "GEL_SetWaitInResetMode(1)");

    /* Cause the IcePick to assert a System Reset */
    GEL_Reset ();
}

/**
 * @brief Called from AM3352_SOM_ethernet.gel to wait for the LAN8710A Ethernet phys to be configured correctly
 * @details If the LAN8710A Ethernet phys are not configured correctly, peforms another system reset and then checks again
 */
reset_if_lan8710_configuration_incorrect ()
{
    while (!GEL_EvalOnTarget ("CortxA8", "check_lan8710a_configuration()"))
    {
        GEL_EvalOnTarget ("CortxA8", "GEL_Disconnect()");
        GEL_EvalOnTarget ("CortxA8", "GEL_SetWaitInResetMode(1)");
        GEL_Reset ();
        GEL_EvalOnTarget ("CortxA8", "GEL_Connect()");
    }
}